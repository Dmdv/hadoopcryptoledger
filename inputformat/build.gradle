apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'

sourceCompatibility = 1.7
version = '1.0.0'
compileJava.options.encoding = 'UTF-8'

group = "com.github.zuinnote"
archivesBaseName = "hadoopcryptoledger-fileformat"
version = "1.0.0"

jar {
    manifest {
        attributes 'Implementation-Title': 'ZuInnoTe - Hadoop CryptoLedger Analytics Library - Inputformat', 'Implementation-Version': version
    } 
    baseName = 'hadoopcryptoledger-fileformat'
}



repositories {
    mavenCentral()
    mavenLocal()
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled true
    }
}

configurations {
	provided
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

javadoc.classpath += configurations.provided


task hadoopCryptoLedgerJavaDocs(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives hadoopCryptoLedgerJavaDocs, sourcesJar
}




dependencies {
    // hadoop libraries
   provided("org.apache.hadoop:hadoop-common:2.7.0")
   provided("org.apache.hadoop:hadoop-mapreduce-client-common:2.7.0")
   // log4j2
   provided("org.apache.logging.log4j:log4j-api:2.4.1")
   // test
   testCompile group: 'junit', name: 'junit', version: '4.12'
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: repositoryUrl) {
        authentication(userName: repositoryUsername, password: repositoryPassword)
      }

      snapshotRepository(url: snapshotRepositoryUrl) {
        authentication(userName: repositoryUsername, password: repositoryPassword)
      }

      pom.project {
        name 'HadoopCryptoLedger'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'HadoopCryptoLedger library to analyse cryptoledgers, such as the Bitcoin blockchain, by using Big Data technologies, such as Hadoop/Hive/Spark'
        url 'https://github.com/ZuInnoTe/hadoopcryptoledger'

        scm {
          connection 'scm:git:https://github.com/ZuInnoTe/hadoopcryptoledger.git'
          developerConnection 'scm:git:https://github.com/ZuInnoTe/hadoopcryptoledger.git'
          url 'https://github.com/ZuInnoTe/hadoopcryptoledger.git'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'jornfranke'
            name 'JÃ¶rn Franke'
            email 'jornfranke@gmail.com'
          }
        }
      }
    }
  }
}



